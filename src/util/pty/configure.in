AC_INIT(getpty.c)
AC_CONFIG_AUX_DIRS($srcdir/../../config $srcdir)
AC_PROG_AWK
dnl libpty.h -> [$](BUILDTOP)/include
AC_PROG_INSTALL
AC_CHECK_PROG(ARCHIVE, ar, ar cqv)
AC_PROG_RANLIB
AC_CHECK_FUNCS(fchmod fchown revoke vhangup killpg _getpty)
dnl
LOGINLIBS=
dnl
dnl Make our operating system-specific security checks and definitions for
dnl login.
dnl  In addition, the following code decides what streams modules will
dnl be pushed onto a pty.In particular, if HAVE_STREAMS is defined and
dnl HAVE_LINE_PUSH is not defined, modules may be pushed by inserting
dnl An appropriate generic ifdef for each module in init_slave.c and
dnl  AC_DEFINES for the operating systems that need the modules.
dnl  Each OS that supports streams has a different idea of what you want to
dnl push.
dnl
AC_CANONICAL_SYSTEM
case $host in
*-*-ultrix*)
echo "Disabling initial vhangup and setsid because they break under Ultrix..."
AC_DEFINE([OPEN_CTTY_ONLY_ONCE],[1])
ac_cv_func_setsid=no # setsid doesn't do the right thing under Ultrix even though present
;;

*-*-aix3*) # AIX has streams include files but not streams TTY
# Moreover, strops.h trashes sys/ioctl.h
krb5_cv_has_streams=no
;;
alpha-dec-osf*)
	AC_CHECK_LIB(security,main,
		AC_DEFINE(HAVE_SETLUID)
		LOGINLIBS="$LOGINLIBS -lsecurity"
	)
	;;
*-*-solaris*)
     AC_DEFINE(PUSH_PTEM)
     AC_DEFINE(PUSH_LDTERM)
     AC_DEFINE(PUSH_TTCOMPAT)
     ;;
*-*-hpux*)
     krb5_cv_has_streams=no
     ;;
esac
dnl
AC_SUBST(LOGINLIBS)
dnl
AC_TYPE_MODE_T
AC_CHECK_FUNCS(getutent setreuid gettosbyname setsid ttyname line_push ptsname grantpt openpty logwtmp)
AC_CHECK_HEADERS(unistd.h stdlib.h string.h utmpx.h utmp.h sys/filio.h sys/sockio.h sys/label.h sys/tty.h ttyent.h lastlog.h sys/select.h sys/ptyvar.h)
AC_CHECK_HEADERS(sys/wait.h)
AC_CHECK_FUNCS(waitpid)
dnl not needed DECLARE_SYS_ERRLIST
KRB5_SIGTYPE
CHECK_SIGNALS
CHECK_WAIT_TYPE
AC_HEADER_CHECK(termios.h,AC_FUNC_CHECK(cfsetispeed,AC_DEFINE(POSIX_TERMIOS)))
CHECK_UTMP
dnl
dnl
AC_CACHE_CHECK([for streams interface], krb5_cv_has_streams,
[AC_TRY_COMPILE([#include <sys/stream.h>
#include <sys/stropts.h>], ,
krb5_cv_has_streams=yes, krb5_cv_has_streams=no)])
if test $krb5_cv_has_streams = yes; then
  AC_DEFINE(HAVE_STREAMS)
fi
dnl
dnl
AC_CACHE_CHECK([arguments to getpgrp], krb5_cv_getpgrp_args,
[AC_TRY_COMPILE([#ifndef __STDC__
#define __STDC__ 1
#endif
#include <unistd.h>
#include <sys/types.h>], [pid_t pid = getpgrp(getpid())],
krb5_cv_getpgrp_args=pid, krb5_cv_getpgrp_args=void)])
if test $krb5_cv_getpgrp_args = pid; then
  AC_DEFINE(GETPGRP_ONEARG)
fi
dnl
dnl
dnl ???? ADD_DEF(-DKERBEROS)
AC_C_CONST
dnl [not yet] V5_USE_SHARED_LIB
AC_OUTPUT(Makefile)
