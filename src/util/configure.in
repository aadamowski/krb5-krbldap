AC_INIT(configure.in)
CONFIG_RULES
AC_PROG_ARCHIVE
AC_PROG_ARCHIVE_ADD
if test $krb5_cv_prog_gcc = yes ; then
     HAVE_GCC=yes
     else HAVE_GCC=
fi

AC_SUBST(HAVE_GCC)
HOST_TYPE=$krb5_cv_host
AC_SUBST(HOST_TYPE)
case $HOST_TYPE in
*-*-aix*)
  AIX_BINCMDS=aix.bincmds
  ;;
esac
AC_SUBST(AIX_BINCMDS)
SHLIB_TAIL_COMP=$krb5_cv_shlibs_tail_comp
AC_SUBST(SHLIB_TAIL_COMP)

dbdir=
WITH_ANAME_DB
if test "$dbval" = db; then
 db=berk_db
else
 WITH_KDB_DB
 if test "$dbval" = db; then db=berk_db; fi
fi

dnl used by several things
AC_PROG_AWK
AC_PROG_RANLIB
AC_PROG_ARCHIVE
AC_PROG_ARCHIVE_ADD

dnl BEG et/configure.in
dnl
dnl Fancy caching of perror result...
AC_CACHE_CHECK([for perror declaration], krb5_cv_decl_perror,
[AC_HEADER_EGREP(perror, errno.h, 
  krb5_cv_decl_perror=yes, krb5_cv_decl_perror=no)])
if test $krb5_cv_decl_perror = yes; then
  AC_DEFINE(PERROR_DECLARED)
fi

AC_CHECK_FUNCS(strerror)

AC_CACHE_CHECK([for sys_errlist declaration], krb5_cv_decl_sys_errlist,
[AC_TRY_COMPILE([#include <stdio.h>
#include <errno.h>], [1+sys_nerr;],
krb5_cv_decl_sys_errlist=yes, krb5_cv_decl_sys_errlist=no)])
# assume sys_nerr won't be declared w/o being in libc
if test $krb5_cv_decl_sys_errlist = yes; then
  AC_DEFINE(SYS_ERRLIST_DECLARED)
  AC_DEFINE(HAVE_SYS_ERRLIST)
fi

if test $krb5_cv_decl_sys_errlist = no; then
  AC_CACHE_CHECK([for sys_errlist in libc], krb5_cv_var_sys_errlist,
  [AC_TRY_LINK( , [1+sys_nerr;],
  krb5_cv_var_sys_errlist=yes, krb5_cv_var_sys_errlist=no)])
  if test $krb5_cv_var_sys_errlist = yes; then
    AC_DEFINE(HAVE_SYS_ERRLIST)
  fi
fi

AC_HEADER_STDARG
AC_HAVE_HEADERS(stdlib.h)
dnl
dnl END et/configure.in

dnl BEG ss/configure.in
dnl
dnl not needed for shell script!
dnl AC_PROG_YACC
dnl HAVE_YYLINENO
dnl AC_FUNC_CHECK(strdup,AC_DEFINE(HAS_STRDUP))
AC_CHECK_HEADERS(dirent.h)
CHECK_WAIT_TYPE
CHECK_SIGNALS
CHECK_SIGPROCMASK
AC_RETSIGTYPE
AC_HEADER_STDARG
AC_CHECK_HEADERS(stdlib.h)
dnl
dnl END ss/configure.in

dnl BEG profile/configure.in
dnl
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_HAVE_FUNCS(stat)
dnl
dnl END profile/configure.in

AC_CONFIG_SUBDIRS(pty)
AC_OUTPUT(Makefile:../config/pre.in+/Makefile.in+../config/post.in
  et/Makefile:../config/pre.in+et/Makefile.in+../config/post.in
  ss/Makefile:../config/pre.in+ss/Makefile.in+../config/post.in
  profile/Makefile:../config/pre.in+profile/Makefile.in+../config/post.in)
